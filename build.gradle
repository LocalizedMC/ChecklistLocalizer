plugins {
    id 'dev.architectury.loom' version '1.7-SNAPSHOT'
    id "com.hypherionmc.modutils.modpublisher" version "2.+"
    id 'maven-publish'
}

group = project.maven_group
version = "$project.mod_version+mc$project.minecraft_version"
base.archivesName = project.archives_name

loom {
    forge {
        mixinConfig 'checklistlocalizer.mixins.json'
    }
}

repositories {
    maven {
        url "https://cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }
}

dependencies {
    minecraft "net.minecraft:minecraft:$project.minecraft_version"
    mappings "net.fabricmc:yarn:$project.yarn_mappings:v2"
    forge "net.minecraftforge:forge:$project.forge_version"

    modImplementation("curse.maven:checklist-405211:$project.checklist_id")
}

processResources {
    inputs.property 'version', project.version

    filesMatching('META-INF/mods.toml') {
        expand version: project.version
    }
}

java {
    withSourcesJar()

    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = 17
}

// Configure Maven publishing.
publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        // Add repositories to publish to here.
        // Notice: This block does NOT have the same function as the block in the top level.
        // The repositories here will be used for publishing your artifact, not for
        // retrieving dependencies.
    }
}

publisher {
    apiKeys {
        curseforge = System.getenv("CURSEFORGE_TOKEN")
        modrinth = System.getenv("MODRINTH_TOKEN")
    }

    setCurseID("$project.curseforge_id")
    setModrinthID("$project.modrinth_id")
    setVersionType("alpha")
    setChangelog(file("CHANGELOG.md").getText("UTF-8"))
    setVersion("$project.version")
    setDisplayName("$project.version")
    setGameVersions("1.20.1")
    setLoaders(loom.platform.get().id())
    setCurseEnvironment("client")
    setArtifact(remapJar)
    addAdditionalFile(sourcesJar)
    curseDepends.required("checklist")
}
